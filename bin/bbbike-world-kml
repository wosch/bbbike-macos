#!/usr/bin/perl
# (c) 2009 Wolfram Schneider
#
# bbbike-world-kml - create an Google Earth KML file for all bbbike@world cities
#
# $Id: bbbike-world-kml,v 1.10 2009/04/11 11:18:52 wosch Exp $

use strict;
use warnings;

sub extract_coords_from_makefile {

    my @data;

    while (<>) {
        next if /^\s*#/;

        chomp;

        if (m,\${OSM_DIR}/(\S+)\s+--\s+(\S+)\s+(\S+),) {
            my $city = $1;
            my $x    = $2;
            my $y    = $3;

            push( @data, [ $city, $x, $y ] );
        }
    }

    return @data;
}

sub bbbike2kml_header {
    print <<EOF;
<?xml version="1.0" encoding="UTF-8"?>
<kml xmlns="http://earth.google.com/kml/2.2">
<Document>
  <name>BBBike @ World</name>
  <description><![CDATA[List of all Cities supported by BBBike @ World]]></description>
  <Style id="style1">
    <IconStyle>
      <Icon>
        <href>http://maps.google.com/mapfiles/ms/micons/pink-pushpin.png</href>
      </Icon>
    </IconStyle>
  </Style>
EOF
}

sub bbbike2kml_footer {
    print <<EOF;
</Document>
</kml>
EOF
}

sub bbbike2kml_placemarks {
    my @data = @_;

    # list of non-German cities with link to English bbbike.en.cgi
    my %en = map { $_ => 1 }
      qw/Austin Cambridge Copenhagen New_York Portland Providence San_Francisco Santa_Cruz Paris London Turin Boulder Barcelona Seattle Chicago PaloAlto CraterLake Davis CambridgeMa Vancouver Miami SanktPetersburg Helsinki Stockholm Oslo Dublin Sofia Kiew Montreal Aarhus/;

    foreach my $c ( sort { $a->[0] cmp $b->[0] } @data ) {
        my ( $city, $x, $y ) = @$c;

        my $bbbike_url = "http://bbbike.elsif.de/cgi/$city"
          . ( exists $en{$city} ? '.en.cgi' : '.cgi' );

        print <<EOF;
  <Placemark>
    <name>$city</name>
    <description><![CDATA[<span style="color:rgb(0, 0, 238);text-decoration:underline"><a href="$bbbike_url">Start BBBike @ $city</a></span>]]></description>
    <styleUrl>#style1</styleUrl>
    <Point>
      <coordinates>$x,$y,0.000000</coordinates>
    </Point>
  </Placemark>
EOF
    }
}

sub bbbike2kml {
    my @data = @_;

    &bbbike2kml_header;
    &bbbike2kml_placemarks(@data);
    &bbbike2kml_footer;
}

my @data = &extract_coords_from_makefile;

&bbbike2kml(@data);

